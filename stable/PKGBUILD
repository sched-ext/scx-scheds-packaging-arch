# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.16
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
options=(!lto)
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        '0001-etc-default-scx-drop-c-based-schedulers.patch')
sha256sums=('4b0355b523c0245f03340c90593c934f876cdfe33145e9c55c20c80bd13b346f'
            'a08ad34b503d5292a97a48152f76106c18b53c25d9e0829f85b5078567656f73')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet <void@manifault.com>
  A8775C60262EEF2AA34A864F5DAD005C4BFEA770  # Jake Hilton <jake@hillion.co.uk>
)

prepare() {
 cd $_gitname
  # Update /etc/default/scx
  patch -Np1 < ../0001-etc-default-scx-drop-c-based-schedulers.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --frozen
}

check() {
  cd $_gitname
  cargo test --frozen --locked
}

package() {
  cd $_gitname

  # binary files
  find target/release -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd services
  install -Dm644 services/systemd/scx_loader.service \
    "$pkgdir/usr/lib/systemd/system/scx_loader.service"

  install -Dm644 services/systemd/scx.service \
    "$pkgdir/usr/lib/systemd/system/scx.service"

  # dbus services
  install -Dm644 services/systemd/org.scx.Loader.service\
    "$pkgdir/usr/share/dbus-1/system-services/org.scx.Loader.service"

  # dbus config
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    "$pkgdir/usr/share/dbus-1/system.d/org.scx.Loader.conf"

  # scx_loader config
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"

  # scx config
  install -Dm644 services/scx \
    "$pkgdir/etc/default/scx"
}
