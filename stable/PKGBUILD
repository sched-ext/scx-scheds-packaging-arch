# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Tejun Heo <tj@kernel.org>
# Contributor: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.15
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
options=(!lto)
source=("git+https://github.com/sched-ext/scx#tag=v${pkgver}"
        "https://patch-diff.githubusercontent.com/raw/sched-ext/scx/pull/2624.patch")
sha256sums=('e2966b2915c9f8f09204bf23710cdb2089c66c0cf953916208a94d64c853fc43'
            'efdbb41f49addae1b4672b1b8b90639762a4e1ea53ce62fe2b9cfd1bbe14c355')

prepare() {
  cd $_gitname
  # Fix scx being enabled as default
  patch -Np1 < ../2624.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --frozen
}

check() {
  cd $_gitname
  cargo test --frozen --locked
}

package() {
  cd $_gitname

  # binary files
  find target/release -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd services
  install -Dm644 services/systemd/scx_loader.service \
    "$pkgdir/usr/lib/systemd/system/scx_loader.service"

  install -Dm644 services/systemd/scx.service \
    "$pkgdir/usr/lib/systemd/system/scx.service"

  # dbus services
  install -Dm644 services/systemd/org.scx.Loader.service\
    "$pkgdir/usr/share/dbus-1/system-services/org.scx.Loader.service"

  # dbus config
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    "$pkgdir/usr/share/dbus-1/system.d/org.scx.Loader.conf"

  # scx_loader config
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"

  # scx config
  install -Dm644 services/scx \
    "$pkgdir/etc/default/scx"
}
